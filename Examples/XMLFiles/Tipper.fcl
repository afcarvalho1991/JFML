// Block definition (there may be more than one block per file)
FUNCTION_BLOCK tipper

// Define input variables
VAR_INPUT
    food : REAL;
    service : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
    tip : REAL;
END_VAR

// Fuzzify input variable 'food'
FUZZIFY food
    TERM rancid := (0, 0) (2, 1) (5.5,0);
    TERM delicious := (5.5,0) (10,1);
END_FUZZIFY

// Fuzzify input variable 'service'
FUZZIFY service
    TERM poor := (0, 1) (2, 0) ; 
    TERM good := (1, 0) (4,0.9) (5,0.9) (6,0.9) (9,0);
    TERM excellent := (8, 0) (10, 1);
END_FUZZIFY

// Defzzzify output variable 'tip'
DEFUZZIFY tip
    TERM cheap := 5;
    TERM average := 10;
    TERM generous := 15;
    // Use 'Center Of Gravity' defuzzification method
    METHOD : COG;
	
    RANGE := (0 .. 20) 
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    // Use 'min' activation method
    ACT : MIN;
    // Use 'max' accumulation method
    ACCU : MAX;

    RULE 1 : IF food IS rancid OR service IS poor
                THEN tip IS cheap;

    RULE 2 : IF service IS good 
                THEN tip IS average; 

    RULE 3 : IF service IS excellent OR food IS delicious
                THEN tip is generous;
END_RULEBLOCK

END_FUNCTION_BLOCK